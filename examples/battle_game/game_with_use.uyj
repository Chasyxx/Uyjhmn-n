// Alternative version...
// idea for this interpreter: Extensions!
// I know, ruins the point of uyjhmn n even more, but that's the point.
// "USE:" activates an extension

// STRINGPRINT is the extension that gives the "PRINT THE STRING" Command.
USE: STRINGPRINT
// Quickvar replaces the delaration, opening, and assignment into one easy CREATE command.
USE: QUICKVAR
PRINT THE STRING ``\e[2JDrexel vs Creaturey: A Turn-Based Battle Game!\n``

CREATE THE VARIABLE CreatHP AS 15
CREATE THE VARIABLE DrexelHP AS 30
CREATE THE VARIABLE Magic AS 5
CREATE THE VARIABLE Damage1 AS -2
CREATE THE VARIABLE Damage2 AS -5
CREATE THE VARIABLE HPRec AS 6
CREATE THE VARIABLE M1 AS -1
CREATE THE VARIABLE 1 AS 1
CREATE THE VARIABLE A AS 65
CREATE THE VARIABLE H AS 72
CREATE THE VARIABLE M AS 77

DECLARE THE NEW VARIABLE COMMAND

DEFINE THE NEW LABEL StartAttack

PRINT THE STRING ``Creaturey's HP: ``
OPEN THE VARIABLE CreatHP
PRINT THE OPEN VARIABLE'S VALUE

PRINT THE STRING ``\nCreaturey's MP: ``
OPEN THE VARIABLE Magic
PRINT THE OPEN VARIABLE'S VALUE

PRINT THE STRING ``\nDrexel's HP: ``
OPEN THE VARIABLE DrexelHP
PRINT THE OPEN VARIABLE'S VALUE

PRINT THE STRING ``\n\n``

OPEN THE VARIABLE COMMAND

PRINT THE STRING ``What will Creaturey do? (A)ttack, (M)agic, (H)eal\n``

GET INPUT AND STORE INTO OPEN VARIABLE AS A CHARACTER
PRINT THE STRING ``\e[2JCreaturey ``
JUMP TO CreatAttack IF COMMAND IS EQUAL TO A
JUMP TO CreatHeal IF COMMAND IS EQUAL TO H
JUMP TO CreatMagic IF COMMAND IS EQUAL TO M

DEFINE THE NEW LABEL CreatAttack
PRINT THE STRING ``\e[31mATTACKS!\e[0m\n``
OPEN THE VARIABLE DrexelHP
ADD Damage1 TO THE OPEN VARIABLE
JUMP TO DrexAttack IF A IS EQUAL TO A

DEFINE THE NEW LABEL CreatMagic
PRINT THE STRING ``uses \e[33mMAGIC!\e[0m\n``
JUMP TO MagicFail IF Magic IS LESS THAN 1
OPEN THE VARIABLE DrexelHP
ADD Damage2 TO THE OPEN VARIABLE
OPEN THE VARIABLE Magic
ADD M1 TO THE OPEN VARIABLE
JUMP TO DrexAttack IF A IS EQUAL TO A

DEFINE THE NEW LABEL CreatHeal
PRINT THE STRING ``\e[32mHEALS!\e[0m\n``
JUMP TO MagicFail IF Magic IS LESS THAN 1
OPEN THE VARIABLE CreatHP
ADD HPRec TO THE OPEN VARIABLE
OPEN THE VARIABLE Magic
ADD M1 TO THE OPEN VARIABLE
JUMP TO DrexAttack IF A IS EQUAL TO A

DEFINE THE NEW LABEL MagicFail
PRINT THE STRING ``\e[34mBut he's out of MP...\e[0m\n``
JUMP TO DrexAttack IF A IS EQUAL TO A

DEFINE THE NEW LABEL DrexAttack
OPEN THE VARIABLE CreatHP
ADD Damage1 TO THE OPEN VARIABLE
JUMP TO WIN IF DrexelHP IS LESS THAN 1
JUMP TO LOSE IF CreatHP IS LESS THAN 1
JUMP TO StartAttack IF A IS EQUAL TO A

DEFINE THE NEW LABEL LOSE
PRINT THE STRING ``Drexel \e[31mATTACKS!\e[0m\nCreaturey \e[31mwas defeated...\e[0m\n``
END THIS PROGRAM
DEFINE THE NEW LABEL WIN
PRINT THE STRING ``Creaturey \e[32mWINS!\e[0m\n``
END THIS PROGRAM