// Alternative version...
// idea for this interpreter: Extensions!
// I know, ruins the point of uyjhmn n even more, but that's the point.
// "USE:" activates an extension

// STRINGPRINT is the extension that gives the "PRINT THE STRING" Command.
USE: STRINGPRINT
// Quickvar replaces the delaration, opening, and assignment into one easy CREATE command.
USE: QUICKVAR
// Random allows for RNG!
USE: RANDOM
PRINT THE STRING ``\e[2JDrexel vs Creaturey: A Turn-Based Battle Game!\n``

CREATE THE VARIABLE CreatHP AS 15
CREATE THE VARIABLE DrexelHP AS 30
CREATE THE VARIABLE CreatMP AS 5
CREATE THE VARIABLE DrexelMP AS 3
CREATE THE VARIABLE Damage1 AS -2
CREATE THE VARIABLE Damage2 AS -5
CREATE THE VARIABLE HPRec AS 6
CREATE THE VARIABLE MPRec AS 0
CREATE THE VARIABLE M1 AS -1
CREATE THE VARIABLE 1 AS 1
CREATE THE VARIABLE A AS 65
CREATE THE VARIABLE H AS 72
CREATE THE VARIABLE M AS 77

DECLARE THE NEW VARIABLE COMMAND

JUMP TO StartAttack IF A IS EQUAL TO A

DEFINE THE NEW LABEL RecoverMP
PRINT THE STRING ``\e[36mCreaturey recovered 1 MP!\e[0m\n``
OPEN THE VARIABLE CreatMP
ADD 1 TO THE OPEN VARIABLE
OPEN THE VARIABLE MPRec
ASSIGN 0 TO THE OPEN VARIABLE

DEFINE THE NEW LABEL StartAttack

OPEN THE VARIABLE MPRec
ADD 1 TO THE OPEN VARIABLE
JUMP TO RecoverMP IF MPRec IS EQUAL TO HPRec

PRINT THE STRING ``Creaturey's HP: ``
OPEN THE VARIABLE CreatHP
PRINT THE OPEN VARIABLE'S VALUE

PRINT THE STRING ``\nCreaturey's MP: ``
OPEN THE VARIABLE CreatMP
PRINT THE OPEN VARIABLE'S VALUE

PRINT THE STRING ``\nDrexel's HP: ``
OPEN THE VARIABLE DrexelHP
PRINT THE OPEN VARIABLE'S VALUE

PRINT THE STRING ``\nDrexel's MP: ``
OPEN THE VARIABLE DrexelMP
PRINT THE OPEN VARIABLE'S VALUE

PRINT THE STRING ``\n\n``

JUMP TO LOSE IF CreatHP IS LESS THAN 1
OPEN THE VARIABLE COMMAND

PRINT THE STRING ``What will Creaturey do? (A)ttack, (M)agic, (H)eal\n``

GET INPUT AND STORE INTO OPEN VARIABLE AS A CHARACTER
PRINT THE STRING ``\e[2JCreaturey ``
JUMP TO CreatAttack IF COMMAND IS EQUAL TO A
JUMP TO CreatHeal IF COMMAND IS EQUAL TO H
JUMP TO CreatCreatMP IF COMMAND IS EQUAL TO M

DEFINE THE NEW LABEL CreatAttack
PRINT THE STRING ``\e[31mATTACKS!\e[0m\n``
OPEN THE VARIABLE DrexelHP
ADD Damage1 TO THE OPEN VARIABLE
JUMP TO StartDrexAttack IF A IS EQUAL TO A

DEFINE THE NEW LABEL CreatCreatMP
PRINT THE STRING ``uses \e[33mMagic!\e[0m\n``
JUMP TO CreatMPFail IF CreatMP IS LESS THAN 1
OPEN THE VARIABLE DrexelHP
ADD Damage2 TO THE OPEN VARIABLE
OPEN THE VARIABLE CreatMP
ADD M1 TO THE OPEN VARIABLE
JUMP TO StartDrexAttack IF A IS EQUAL TO A

DEFINE THE NEW LABEL CreatHeal
PRINT THE STRING ``\e[32mHEALS!\e[0m\n``
JUMP TO CreatMPFail IF CreatMP IS LESS THAN 1
OPEN THE VARIABLE CreatHP
ADD HPRec TO THE OPEN VARIABLE
OPEN THE VARIABLE CreatMP
ADD M1 TO THE OPEN VARIABLE
JUMP TO StartDrexAttack IF A IS EQUAL TO A

DEFINE THE NEW LABEL CreatMPFail
PRINT THE STRING ``\e[34mBut he's out of MP...\e[0m\n``
JUMP TO StartDrexAttack IF A IS EQUAL TO A

DEFINE THE NEW LABEL StartDrexAttack
JUMP TO WIN IF DrexelHP IS LESS THAN 1
OPEN THE VARIABLE COMMAND
PUT A RANDOM NUMBER BETWEEN 0 AND 2 INTO THE OPEN VARIABLE
JUMP TO DrexAttack IF COMMAND IS LESS THAN 1
JUMP TO DrexAttack IF DrexelMP IS LESS THAN 1
JUMP TO DrexMagic IF COMMAND IS EQUAL TO 1
JUMP TO DrexHeal IF COMMAND IS GREATER THAN 1

DEFINE THE NEW LABEL DrexAttack
PRINT THE STRING ``Drexel \e[31mATTACKS!\e[0m\n``
OPEN THE VARIABLE CreatHP
ADD Damage1 TO THE OPEN VARIABLE
JUMP TO StartAttack IF A IS EQUAL TO A

DEFINE THE NEW LABEL DrexMagic
PRINT THE STRING ``Drexel uses \e[31mMAGIC!\e[0m\n``
OPEN THE VARIABLE CreatHP
ADD Damage2 TO THE OPEN VARIABLE
OPEN THE VARIABLE DrexelMP
ADD M1 TO THE OPEN VARIABLE
JUMP TO StartAttack IF A IS EQUAL TO A

DEFINE THE NEW LABEL DrexHeal
PRINT THE STRING ``Drexel \e[31mHEALS!\e[0m\n``
OPEN THE VARIABLE DrexelHP
ADD HPRec TO THE OPEN VARIABLE
OPEN THE VARIABLE DrexelMP
ADD M1 TO THE OPEN VARIABLE
JUMP TO StartAttack IF A IS EQUAL TO A

DEFINE THE NEW LABEL LOSE
PRINT THE STRING ``Creaturey \e[31mwas defeated...\e[0m\n``
END THIS PROGRAM
DEFINE THE NEW LABEL WIN
PRINT THE STRING ``Creaturey \e[32mWINS!\e[0m\n``
END THIS PROGRAM